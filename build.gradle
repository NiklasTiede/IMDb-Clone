plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'com.github.ben-manes.versions' version '0.51.0'
}

group = 'com.thecodinglab'
version = '1.0.0'

compileJava.dependsOn 'spotlessApply'

java {
	sourceCompatibility = JavaVersion.VERSION_21
}

idea {
	project {
		languageLevel = JavaVersion.VERSION_21
	}
	module {
		jdkName = JavaVersion.VERSION_21
	}
}

repositories {
	mavenCentral()
}

spotless {
	java {
		target 'src/main/java/**/*.java'
		targetExclude 'build/**', 'frontend/**'
		targetExclude 'src/main/java/**/payload/*.java', 'src/main/java/**/payload/**/*.java'
		importOrder()
		removeUnusedImports()
		trimTrailingWhitespace()
		googleJavaFormat()
		toggleOffOn()
	}
}

dependencies {
	//-- Improved developing
//	implementation "org.springframework.boot:spring-boot-devtools:${springBootVersion}"

	//-- Servlet
	implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"

	//-- Validation
	implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"

	//-- Documentation - Swagger UI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

	//-- Start/stop containers with startup
//	implementation "org.springframework.boot:spring-boot-docker-compose:${springBootVersion}"

	//-- Mapping
	implementation 'org.mapstruct:mapstruct:1.6.0.Beta1'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0.Beta1'

	//-- Rel. Database
	implementation 'mysql:mysql-connector-java:8.0.33'
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"

	//-- Shared Cache
	implementation "org.springframework.boot:spring-boot-starter-data-redis:${springBootVersion}"

	//-- SearchEngine
	implementation 'org.springframework.data:spring-data-elasticsearch:5.2.3'

	//-- File Storage
	implementation 'io.minio:minio:8.5.8'

	//-- Email
	implementation "org.springframework.boot:spring-boot-starter-mail:${springBootVersion}"

	//-- Security: JWT / OAuth2
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	implementation 'org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE'

	//-- Monitoring
	implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
	implementation 'io.micrometer:micrometer-registry-prometheus:1.12.3'

	//-- Logging
	implementation "org.springframework.boot:spring-boot-starter-logging:${springBootVersion}"
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

	//-- Testing
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
	testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'

	testImplementation 'io.rest-assured:rest-assured:5.4.0'

	testImplementation 'org.testcontainers:testcontainers:1.19.5'
	testImplementation 'org.testcontainers:junit-jupiter:1.19.5'
}

test {
	useJUnitPlatform()
}
