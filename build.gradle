plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'com.github.ben-manes.versions' version '0.51.0'
	id "org.openrewrite.rewrite" version "6.11.2"
}

rewrite {
//	activeRecipe("org.openrewrite.java.spring.boot3.SpringBoot3BestPractices")
//	activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_2")
//	activeRecipe("org.openrewrite.java.spring.boot3.SpringBootProperties_3_2")
//	activeRecipe("org.openrewrite.java.dependencies.DependencyVulnerabilityCheck")
//	activeRecipe("org.openrewrite.recipes.JavaRecipeBestPractices")
}

group = 'com.thecodinglab'
version = '1.0.0'

compileJava.dependsOn 'spotlessApply'


idea {
	project {
		languageLevel = JavaVersion.VERSION_21
	}
	module {
		jdkName = JavaVersion.VERSION_21
	}
}

repositories {
	mavenCentral()
}

spotless {
	java {
		target 'src/main/java/**/*.java', 'src/test/java/**/*.java'
		targetExclude 'build/**', 'frontend/**'
		targetExclude 'src/main/java/**/payload/*.java', 'src/main/java/**/payload/**/*.java'
		importOrder()
		removeUnusedImports()
		trimTrailingWhitespace()
		googleJavaFormat()
		toggleOffOn()
	}
}

dependencies {
//	rewrite("org.openrewrite.recipe:rewrite-spring:5.6.0")
//	rewrite("org.openrewrite.recipe:rewrite-java-dependencies:1.6.0")
//	rewrite("org.openrewrite.recipe:rewrite-recommendations:1.3.1")

	//-- Improved developing
	implementation "org.springframework.boot:spring-boot-devtools:${springBootVersion}"

	//-- Servlet
	implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"

	//-- Validation
	implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"

	//-- Documentation - Swagger UI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0'

	//-- Start/stop containers with startup
	implementation "org.springframework.boot:spring-boot-docker-compose:${springBootVersion}"

	//-- Mapping
	implementation 'org.mapstruct:mapstruct:1.6.0.Beta1'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0.Beta1'

	//-- Rel. Database
	implementation 'com.mysql:mysql-connector-j:8.3.0'
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"

	//-- SearchEngine
	implementation 'org.springframework.data:spring-data-elasticsearch:5.2.4'
	implementation 'org.eclipse.parsson:parsson:1.1.5' // fix transitive vulnerability

	//-- File Storage
	implementation 'io.minio:minio:8.5.9'

	//-- Email
	implementation "org.springframework.boot:spring-boot-starter-mail:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}"

	//-- Security: JWT / OAuth2
	implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"

	implementation "org.springframework.security:spring-security-core:${springVersion}"
	implementation "org.springframework.security:spring-security-web:${springVersion}"
	implementation "org.springframework.security:spring-security-config:${springVersion}"

	//-- Monitoring
	implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
	implementation 'io.micrometer:micrometer-registry-prometheus:1.12.4'

	//-- Logging
	implementation "org.springframework.boot:spring-boot-starter-logging:${springBootVersion}"
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

	//-- Testing
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'

	testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
	testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
	testImplementation "org.testcontainers:mysql:${testContainersVersion}"
	testImplementation "org.testcontainers:elasticsearch:${testContainersVersion}"
	testImplementation "org.testcontainers:minio:${testContainersVersion}"

	if (System.getProperty('os.name').toLowerCase().contains('mac')) {
		runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.108.Final:osx-aarch_64'
	}
}

test {
	useJUnitPlatform()
}
